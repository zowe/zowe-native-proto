         MACRO
.*
.*  This program and the accompanying materials are
.*  made available under the terms of the Eclipse Public License v2.0
.*  which accompanies this distribution, and is available at
.*  https://www.eclipse.org/legal/epl-v20.html
.*
.*  SPDX-License-Identifier: EPL-2.0
.*
.*  Copyright Contributors to the Zowe Project.
.*
.*
.*
.*  Custom prolog intended to be paired with ZWEEPILG to allow
.*  variable sized stack allocations (instead of the default 1MB).
.*
.*  Usage:
.*
.*       ZWEPROLG NEWDSA=[YES|NO],[#KBs]   e.g. NEWDSA=(YES,1024)
.*
.*  Commonly referenced symbols:
.*    &CCN_RHIGH
.*    &CCN_RLOW
.*    &CCN_LP64
.*    &CCN_ASCM
.*    &CCN_MAIN
.*    &CCN_LITN
.*    &CCN_RENT
.*    &CCN_DSASZ
.*    &CCN_CSECT
.*    CCN_IASM_MACRO
.*
&NAME    ZWEPROLG &NEWDSA=
.*
         COPY  CCNZGBL
         GBLA  &DSA_SZ
.*
&MACNM   SETC  '&SYSMAC(0)'
.*
&DSA_SZ  SETA  0
&IS_64   SETB  (&CCN_LP64)
&IS_AR   SETB  ('CCN_ASCM' EQ 'AR')
&KB#     SETA  1024
&GETDSA  SETB  0
.*
.*       Set 31 or 64 bit instructions
.*
         AIF   (&IS_64).INST64
&LHIx    SETC  'LHI '
&MHIx    SETC  'MHI '
&LRxx    SETC  'LR  '
&Lxxx    SETC  'L   '
&ARxx    SETC  'AR  '
&SLRx    SETC  'SLR '
         AGO   .CONT000
.INST64  ANOP  ,
&LHIx    SETC  'LGHI'
&MHIx    SETC  'MGHI'
&LRxx    SETC  'LGR '
&Lxxx    SETC  'LGF '
&ARxx    SETC  'AGR '
&SLRx    SETC  'SLGR'
.CONT000 ANOP  ,
.*
.*       Validate paramters
.*
         AIF   ('&NEWDSA(1)' EQ 'YES').CONT040
         AIF   ('&NEWDSA(1)' EQ '').CONT100
         AIF   ('&NEWDSA(1)' EQ 'NO').CONT100
         AGO   .ERR010
.*
.*       Get DSA path
.*
.CONT040 ANOP ,
&GETDSA  SETB  1
         AIF   ('&NEWDSA(2)' EQ '').CONT100
&KB#     SETA  &NEWDSA(2)
.*
.*       Save input regs
.*
.CONT100 ANOP ,
         AIF   (&IS_64).SAVE64
         STM   14,12,12(13)
         AGO   .CONT400
.SAVE64  ANOP
         STMG  14,12,8(13)
.CONT400 ANOP  ,
         AIF   (NOT &GETDSA).EXIT
.*
.*       Obtain DSA
.*
         LARL  15,&CCN_LITN
         USING &CCN_LITN,15
.*
&DSA_SZ  SETA  &KB#*1024
         &LHIx 3,&KB#
         &MHIx 3,1024
*
         STORAGE OBTAIN,                                               +
               LENGTH=(3),                                             +
               CALLRKY=YES,                                            +
               SP=132,                                                 +
               BNDRY=PAGE,                                             +
               COND=NO
.*
         &LRxx 2,1               -> New storage save
.*
         &LRxx 0,1               -> New storage
         &LRxx 1,3               Storage len
         &SLRx 15,15             No source len
         MVCL  0,14              Clear storage
.*
         &LRxx 15,2              -> New storage
.*
         AIF   (&IS_64).LINK64
         ST    15,8(,13)
         ST    13,4(,15)
         LM    14,14,12(13)
         LM    0,3,20(13)
         AGO   .CONT600
.LINK64  ANOP
         STG   15,136(,13)
         STG   13,128(,15)
         LMG   14,14,8(13)
         LMG   0,3,24(13)
.CONT600 ANOP
         &LRxx 13,15
         DROP  15
         AGO   .EXIT
.*
.*       Errors
.*
.ERR010  ANOP  ,
         MNOTE 8,'&MACNM. - DSA= must be YES|NO'
         AGO   .EXIT
.*
.* Common exit
.*
.EXIT    ANOP  ,
         MEND
