         MACRO
.*
.*  This program and the accompanying materials are
.*  made available under the terms of the Eclipse Public License v2.0
.*  which accompanies this distribution, and is available at
.*  https://www.eclipse.org/legal/epl-v20.html
.*
.*  SPDX-License-Identifier: EPL-2.0
.*
.*  Copyright Contributors to the Zowe Project.
.*
.*
.*
.*  Custom epilog intended to be paired with ZWEPROLG to allow
.*  variable sized stack allocations (instead of the default 1MB).
.*
.*  Usage:
.*
.*       ZWEEPILG
.*
.*  Todo:
.*       - skip free if not &CCN_MAIN
.*
&NAME    ZWEEPILG ,
.*
         COPY  CCNZGBL
         GBLA  &ZG_DSASZ
         GBLA  &ZG_DSASP
         GBLB  &ZG_SAVE
         GBLB  &ZG_BAKR
         GBLB  &ZG_SAM64
.*
&MACNM   SETC  '&SYSMAC(0)'

&IS_64   SETB  (&CCN_LP64 OR &ZG_SAM64)
&IS_AR   SETB  ('&CCN_ASCM' EQ 'AR')
.*
.*       Set 31 or 64 bit instructions
.*
         AIF   (&IS_64).INST64
&LRx     SETC  'LR '
         AGO   .CONT020
.INST64  ANOP  ,
&LRx     SETC  'LGR'
.CONT020 ANOP  ,
.*
         AIF   (&ZG_DSASZ EQ 0).CONT600
         AIF   (NOT &ZG_BAKR).CONT300
         &LRx  2,15            Preserve R15
         LHI   0,3             Code #3 = MSTA
         ESTA  0,0
         AGO   .CONT400
.CONT300 ANOP ,
         &LRx  1,13            -> Dynamic storage
.*
         AIF   (&IS_64).LOAD64
         L     13,4(,13)       Get previous save area
         ST    15,16(,13)      Save R15 contents there
         AGO   .CONT400
.LOAD64  ANOP  ,
         LG    13,128(,13)     Get previous save area
         STG   15,16(,13)      Save R15 contents there
.CONT400 ANOP  ,
*
         LARL  15,&CCN_LITN    -> Literals
         USING &CCN_LITN,15
*
         STORAGE RELEASE,                                              +
               CALLRKY=YES,                                            +
               SP=&ZG_DSASP.,                                          +
               LENGTH=&ZG_DSASZ,                                       +
               ADDR=(1)
*
         DROP  15
.*
         AIF   (NOT &ZG_BAKR).CONT500
         &LRx  15,2            Restore R15
         PR    ,               Return to caller
         AGO   .EXIT
.*
.*       Restore R15 from SA if we released storage
.*
.CONT500 ANOP
         AIF   (&IS_64).CONT540
         L     15,16(,13)      Restore R15 from SA
         AGO   .CONT600
.CONT540 ANOP
         LG    15,16(,13)      Restore R15 from SA
.*
.*       Exit linkage
.*
.CONT600 ANOP
         AIF   (&IS_64).ULINK64
         L     14,12(,13)      Restore R14 from save area
         LM    1,12,24(13)     Restore remaining regs
         AGO   .CONT800
.ULINK64 ANOP
         LG    14,8(,13)       Restore R14 from save area
         LMG   1,12,32(13)     Restore remaining regs
.CONT800 ANOP
         BR    14              Return to caller
.*
.* Common exit
.*
.EXIT    ANOP  ,
         MEND
