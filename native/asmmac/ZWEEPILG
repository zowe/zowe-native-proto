         MACRO
.*
.*  This program and the accompanying materials are
.*  made available under the terms of the Eclipse Public License v2.0
.*  which accompanies this distribution, and is available at
.*  https://www.eclipse.org/legal/epl-v20.html
.*
.*  SPDX-License-Identifier: EPL-2.0
.*
.*  Copyright Contributors to the Zowe Project.
.*
.*
.*
.*  Custom epilog intended to be paired with ZWEPROLG to allow
.*  variable sized stack allocations (instead of the default 1MB).
.*
.*  Usage:
.*
.*       ZWEEPILG
.*
&NAME    ZWEEPILG ,
.*
         COPY  CCNZGBL
         GBLA  &DSA_SZ
.*
&MACNM   SETC  '&SYSMAC(0)'

&IS_64   SETB  (&CCN_LP64)
&IS_AR   SETB  ('CCN_ASCM' EQ 'AR')
.*
.*       Set 31 or 64 bit instructions
.*
         AIF   (&IS_64).INST64
&LRx     SETC  'LR '
         AGO   .CONT020
.INST64  ANOP  ,
&LRx     SETC  'LGR'
.CONT020 ANOP  ,
.*
         AIF   (&DSA_SZ EQ 0).CONT600
         &LRx  1,13
.*
         AIF   (&IS_64).LOAD64
         L     13,4(,13)
         ST    15,16(,13)
         AGO   .CONT400
.LOAD64  ANOP  ,
         LG    13,128(,13)
         STG   15,16(,13)
.CONT400 ANOP  ,
*
         LARL  15,&CCN_LITN
         USING &CCN_LITN,15
*
         STORAGE RELEASE,                                              +
               LENGTH=&DSA_SZ,                                         +
               ADDR=(1)
*
         DROP  15
.*
.*       Restore R15 from SA if we released storage
.*
         AIF   (&IS_64).CONT500
*
         L     15,16(,13)
         AGO   .CONT600
.CONT500 ANOP
         LG    15,16(,13)
.*
.*       Exit linkage
.*
.CONT600 ANOP
         AIF   (&IS_64).ULINK64
         L     14,12(,13)
         LM    1,12,24(13)
         AGO   .CONT800
.ULINK64 ANOP
         LG    14,8(,13)
         LMG   1,12,32(13)
.CONT800 ANOP
         BR    14
.*
.* Common exit
.*
.EXIT    ANOP  ,
         MEND
